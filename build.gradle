buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name 'JitPack'
            url 'https://jitpack.io'
        }
        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath 'com.github.LxGaming:MixinGradle:d75e32e'//0.6-SNAPSHOT'
        classpath "com.github.asbyth:ForgeGradle:6f53277"
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
        classpath 'org.yaml:snakeyaml:1.10'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = '1.0'
group = 'com.natia.secretMod'
archivesBaseName = 'secretmod'

mixin {
    add sourceSets.main, "mixins.secretmod.refmap.json"
}

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = project.mcpVersion
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

shadowJar {
    dependencies {
        include(dependency('info.bliki.wiki:bliki-core:3.1.0'))
        include(dependency('commons-io:commons-io'))
        include(dependency('org.spongepowered:mixin:0.7.11-SNAPSHOT'))
        include(dependency('org.yaml:snakeyaml:1.10'))
    }
    exclude 'dummyThing'
    exclude 'LICENSE.txt'
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

build.dependsOn(shadowJar)

jar {
    manifest.attributes(
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': "mixins.secretmod.json",
            'FMLCorePluginContainsFMLMod': true,
            "ForceLoadAsMod": true
    )
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://repo.spongepowered.org/maven/' }
    flatDir {
        dirs 'deps'
    }
}

dependencies {
    compile('org.spongepowered:mixin:0.7.11-SNAPSHOT')
    annotationProcessor('org.spongepowered:mixin:0.7.11-SNAPSHOT')
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.10'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

moveResources.dependsOn processResources
classes.dependsOn moveResources